apply plugin: 'com.jfrog.bintray'

version = libraryVersion

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "../docs"
}

task sourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc, dependsOn: dokka) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from "../docs"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

String localProperty(propertyName) {
    Properties properties = new Properties()
    File file = project.rootProject.file('local.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
        return properties.getProperty(propertyName)
    }
    return ""
}

bintray {
    user = localProperty("bintrayUser")
    key = localProperty("bintrayApiKey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription

        // If the repository is hosted by an organization instead of personal account.
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        override = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription

            gpg {
                sign = true
                passphrase = localProperty('gpgPassphrase')
            }

            mavenCentralSync {
                sync = true
                user = localProperty('userToken') // OSS user token
                password = localProperty('paasword') // OSS user password
            }
        }
    }
}
